#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout(push_constant) uniform PushConsts {
	vec2 screen_size_rcp;
	float aspect_ratio;
	float time;
} push_constants;

layout(set = 0, binding = 0) uniform sampler2D input_tex;
layout(set = 0, binding = 1, rgb10_a2) uniform writeonly image2D output_image;

layout (local_size_x = 8, local_size_y = 8) in;
void main() 
{
	const float CYCLE_X = 3.14159f * 5.0f;
	const float CYCLE_Y = CYCLE_X * push_constants.aspect_ratio;
	const float AMP_X = 1.0f / 300.0f;
	const float AMP_Y = AMP_X * push_constants.aspect_ratio;

	const float posX = float(gl_GlobalInvocationID.x) * push_constants.screen_size_rcp.x;
	const float posY = float(gl_GlobalInvocationID.y) * push_constants.screen_size_rcp.y;

	const float texX = (posX + (sin(posY * CYCLE_X + push_constants.time) * AMP_X)) * (1.0f - AMP_X * 2.0f) + AMP_X;
	const float texY = (posY + (sin(posX * CYCLE_Y + push_constants.time) * AMP_Y)) * (1.0f - AMP_Y * 2.0f) + AMP_Y;

	vec4 value = texture(input_tex, vec2(texX, texY));
	imageStore(output_image, ivec2(gl_GlobalInvocationID.xy), value);
}
